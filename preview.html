<!doctype html>
<html>
<head>
<meta charset="utf-8"/>
<meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1, user-scalable=0"/>
<meta name="apple-mobile-web-app-capable" content="yes"/>
<meta name="apple-mobile-web-app-status-bar-style" content="black"/>
<title>Lost Treasures</title>
<style>
html, body {
	margin: 0; padding: 0;
	overflow: hidden;
	-webkit-tap-highlight-color: rgba(0,0,0,0);
	-webkit-touch-callout: none;
	-webkit-user-select: none;
	-khtml-user-select: none;
	-moz-user-select: none;
	-ms-user-select: none;
	-ms-touch-action: none;
	user-select: none;
}
canvas {
	position: fixed;
	width: 100%;
	height: 100%;
}
#Text {
	position: fixed;
	width: 100%;
	margin: 0; padding: 1em 0;
	font: 2vw monospace; color: #fff;
	font-weight: bold;
	text-align: center;
	line-height: 200%;
}
#Text a {
	color: #fff;
}
.Warning {
	color: #f22;
}
</style>
</head>
<body>
<canvas id="Canvas">Sorry, this browser cannot render this content.</canvas>
<div id="Text"></div>
<script id="LightVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;

uniform mat4 lightProjMat;
uniform mat4 lightModelViewMat;

void main() {
	gl_Position = lightProjMat * lightModelViewMat * vec4(vertex, 1.0);
}
</script>
<script id="LightFragmentShader" type="x-shader/x-fragment">
precision mediump float;

vec4 encodeFloat(float depth) {
	const vec4 bitShift = vec4(
		256 * 256 * 256,
		256 * 256,
		256,
		1.0
	);
	const vec4 bitMask = vec4(
		0,
		1.0 / 256.0,
		1.0 / 256.0,
		1.0 / 256.0
	);
	vec4 comp = fract(depth * bitShift);
	comp -= comp.xxyz * bitMask;
	return comp;
}

void main() {
	gl_FragColor = encodeFloat(gl_FragCoord.z);
}
</script>
<script id="VertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;
attribute vec3 normal;

uniform mat4 projMat;
uniform mat4 modelViewMat;
uniform mat4 normalMat;
uniform mat4 lightModelViewMat;
uniform mat4 lightProjMat;
uniform vec3 lightDirection;

varying mediump float intensity;
varying mediump float z;
varying vec4 shadowPos;

const mat4 texUnitConverter = mat4(
	0.5, 0.0, 0.0, 0.0,
	0.0, 0.5, 0.0, 0.0,
	0.0, 0.0, 0.5, 0.0,
	0.5, 0.5, 0.5, 1.0
);

void main() {
	gl_Position = projMat * modelViewMat * vec4(vertex, 1.);
	z = gl_Position.z;
	intensity = max(0., dot(normalize(mat3(normalMat) * normal),
		lightDirection));
	shadowPos = texUnitConverter * lightProjMat * lightModelViewMat *
		vec4(vertex, 1.0);
}
</script>
<script id="FragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform float far;
uniform vec4 sky;
uniform vec4 color;
uniform sampler2D shadowDepthTexture;

varying mediump float intensity;
varying mediump float z;
varying vec4 shadowPos;

float decodeFloat(vec4 c) {
	const vec4 bitShift = vec4(
		1.0 / (256.0 * 256.0 * 256.0),
		1.0 / (256.0 * 256.0),
		1.0 / 256.0,
		1
	);
	return dot(c, bitShift);
}

void main() {
	vec3 fragmentDepth = shadowPos.xyz;
	fragmentDepth.z -= 0.007;
	float texelSize = 1.0 / 512.0;
	float amountInLight = 0.0;
	for (int x = -1; x <= 1; ++x) {
		for (int y = -1; y <= 1; ++y) {
			float texelDepth = decodeFloat(texture2D(shadowDepthTexture,
					fragmentDepth.xy + vec2(x, y) * texelSize));
			if (fragmentDepth.z < texelDepth) {
				amountInLight += 1.0;
			}
		}
	}
	amountInLight /= 9.0;
	amountInLight *= intensity;
	float f = z / far;
	gl_FragColor = vec4(
		(1. - f) * color.rgb * (.5 + amountInLight * .5) + f * sky.rgb,
		color.a);
}
</script>
<script id="SeaVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;
attribute vec3 normal;

uniform mat4 projMat;
uniform mat4 modelViewMat;
uniform mat4 normalMat;
uniform vec3 lightDirection;
uniform float time;
uniform float radius;

varying mediump float intensity;
varying mediump float z;

void main() {
	float vx = mod(radius + vertex.x, radius);
	float vy = mod(radius + vertex.y, radius);
	float t = sin(time + vx + vy) * .3;
	gl_Position = projMat * modelViewMat *
		vec4(vertex.x, vertex.y + t, vertex.z, 1.);
	z = gl_Position.z;
	intensity = max(0., dot(normalize(mat3(normalMat) * normal),
		lightDirection));
}
</script>
<script id="SeaFragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform float far;
uniform vec4 sky;
uniform vec4 color;

varying mediump float intensity;
varying mediump float z;

void main() {
	float f = z / far;
	gl_FragColor = vec4(
		(1. - f) * color.rgb * (.5 + intensity * .5) + f * sky.rgb,
		color.a);
}
</script>
<script id="ControlsVertexShader" type="x-shader/x-vertex">
attribute vec3 vertex;

uniform mat4 projMat;
uniform mat4 modelViewMat;

void main() {
	gl_Position = projMat * modelViewMat * vec4(vertex, 1.);
}
</script>
<script id="ControlsFragmentShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform vec4 color;

void main() {
	gl_FragColor = color;
}
</script>
<script src="src.js"></script>
</body>
</html>
